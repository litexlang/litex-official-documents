```litex
# 1. Axiom of extensionality: two sets are equal if and only if they have the same elements

forall x, y set:
    forall t x:
        t $in y
    forall t y:
        t $in x
    =>:
        $equal_set(x, y)
        x = y

forall x, y set:
    x = y
    =>:
        $equal_set(x, y)
        forall t x:
            t $in y
        forall t y:
            t $in x

# 2. Axiom of regularity: every non-empty set has an element that is disjoint from the set

forall x nonempty_set:
    exist y x st y $disjoint_from x

# 3. Axiom schema of specification: we can construct a set from a given set and a property. We use keyword {x parent_set: $p1(..), $p2(..), ...} for this.

forall x {t R: t > 100, t < 1000}: x > 17, x < 1700

# 4. Axiom of pairing: we can construct a set from two given sets. We use {x, y} for this.

forall x {1, 2}: x = 1 or x = 2

# 5. Axiom of union: we can construct a set from a given set of sets. We use keyword union(x) and cup(x) for this.

prove union({1, 2}, {3, 4}) = {1, 2, 3, 4}:
    prove forall x union({1, 2}, {3, 4}): x $in {1, 2, 3, 4}:
        x $in {1, 2} or x $in {3, 4}
        prove_case_by_case:
            =>:
                x $in {1, 2, 3, 4}
            case x $in {1, 2}:
                prove_case_by_case:
                    =>:
                        x $in {1, 2, 3, 4}
                    case x = 1:
                        x $in {1, 2, 3, 4}
                    case x = 2:
                        x $in {1, 2, 3, 4}
            case x $in {3, 4}:
                prove_case_by_case:
                    =>:
                        x $in {1, 2, 3, 4}
                    case x = 3:
                        x $in {1, 2, 3, 4}
                    case x = 4:
                        x $in {1, 2, 3, 4}
    prove forall x {1, 2, 3, 4}: x $in union({1, 2}, {3, 4}):
        prove_case_by_case:
            =>:
                x $in union({1, 2}, {3, 4})g
            case x = 1:
                x $in {1, 2}
                x $in union({1, 2}, {3, 4})
            case x = 2:
                x $in {1, 2}
                x $in union({1, 2}, {3, 4})
            case x = 3:
                x $in {3, 4}
                x $in union({1, 2}, {3, 4})
            case x = 4:
                x $in {3, 4}
                x $in union({1, 2}, {3, 4})
        
# 6. Axiom schema of replacement: the image of a set under any definable function will also fall inside a set

# If A $subset_of X, then if f $in fn(X)Y, f $in fn(A)Y is true by definition.
forall X, Y set, f fn(X) Y:
    {y Y: exist x X st f(x) = y} $subset_of Y

# 7. Axiom of infinity: there exists an infinite set

exist x set st $axiom_of_infinity(x)

# 8. Axiom of power set: for any set, there exists a set of all subsets of the set. We use keyword power_set for this.

forall X set:
    forall y set:
        y $subset_of X
        =>:
            y $in power_set(X)

    forall y power_set(X):
        y $subset_of X
```
